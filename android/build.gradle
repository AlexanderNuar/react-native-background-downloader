apply plugin: 'com.android.library'

def useAndroidX = (project.hasProperty('android.useAndroidX')) ? project.property('android.useAndroidX') : false

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility reasons (for RN < 0.71)
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace "com.eko"
    }

    compileSdkVersion safeExtGet('compileSdkVersion', 34)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 34)
        versionCode 1
        versionName '1.0'
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'
    if (useAndroidX) {
        api 'com.github.tonyofrancis.Fetch:xfetch2:3.1.6'
        implementation 'com.github.tonyofrancis.Fetch:xfetch2okhttp:3.1.6'
    } else {
        api 'com.tonyodev.fetch2:fetch2:3.0.12'
        implementation 'com.tonyodev.fetch2okhttp:fetch2okhttp:3.0.12'
    }
}
